pkgname=buildbox-common
pkgver=1.3.52
pkgrel=1
pkgdesc="Shared protocol-buffer definitions and various helper functions"
arch=(x86_64)
url="https://buildgrid.build"
license=('Apache')
depends=('bubblewrap' 'curl' 'fuse3' 'google-glog' 'grpc' 'openssl' 'protobuf' 'tomlplusplus' 'util-linux')
makedepends=('cmake' 'git' 'ninja' 'tomlplusplus' 'nlohmann-json')
source=("git+https://gitlab.com/BuildGrid/buildbox/buildbox-common#tag=${pkgver}&commit=a880f85ce8904f0555622471badfd952c2c39d6e")
sha256sums=('SKIP')

build() {
  mkdir -p build && cd build
  cmake ../buildbox-common \
    -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DTOOLS=OFF \
    -DCASD=ON \
    -DCASD_BUILD_BENCHMARK=OFF \
    -DFUSE=ON \
    -DRECC=ON \
    -DRUN_BUBBLEWRAP=ON \
    -DWORKER=ON
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir/" ninja install
}

pkgname=buildstream
pkgver=2.4.0
pkgrel=1
pkgdesc="A powerful and flexible software integration toolset"
arch=(x86_64)
url="https://buildstream.build"
license=('Apache')
depends=(
    buildbox-common
    python
    python-click
    python-dateutil
    python-grpcio
    python-jinja
    python-pluginbase
    python-protobuf
    python-psutil
    python-pyroaring
    python-ruamel-yaml
    python-ruamel.yaml.clib
    python-setuptools
    python-ujson
    # specific host site dependencies
    bzip2 gzip lzip tar xz
)
optdepends=(
    "buildstream-plugins: Official BuildStream plugins like “git”"
    "buildstream-plugins-container: Official BuildStream plugin for Docker images"
    "buildstream-plugins-experimental: Official experimental BuildStream plugins"
)
makedepends=(cython git python-packaging)
source=(
    "git+https://github.com/apache/buildstream.git#tag=${pkgver}&commit=e00f22f155b3a61825931bb3a639fef5e2068f34"
    "preexec_fn_removal.patch"
)
sha256sums=('SKIP' '69ed13d465111f383d7952cfd3d62b393176708a1a84d6a8949a7500dfe2fac1')

prepare() {
    cd $pkgname
    patch -Np1 -i ../preexec_fn_removal.patch
}

build() {
  cd buildstream
  python setup.py build
}

package() {
  cd buildstream
  python setup.py install --root="$pkgdir/" --skip-build
}
